#!/usr/bin/env python
import os
import sys

env = SConscript("../../ExternalTools/godot-cpp/SConstruct")

# Check our platform specifics
if env["platform"] == "macos":
    libexportfolder = "/macos/"

elif env["platform"] in ("linuxbsd", "linux"):
    libexportfolder = "/linux/"

elif env["platform"] == "windows":
    libexportfolder = "/windows/"

if env["target"] == "template_debug":
    debugsuffix = "_debug"
else:
    debugsuffix = ""
    
flatbuffer_folder = "../../ExternalTools/flatbuffers/"
flatbuffer_include = [ "include/", "src/", "grpc/" ]
flatbuffer_src = [
	"idl_gen_text.cpp",
	"idl_parser.cpp",
	"reflection.cpp",
	"util.cpp"
]
	
env.Prepend(CPPPATH=[flatbuffer_folder + folder for folder in flatbuffer_include])
env.Append(CPPPATH=["src/"])
sources = Glob("src/*.cpp")
sources.extend([flatbuffer_folder + 'src/' + flatbuffer_src_file for flatbuffer_src_file in flatbuffer_src])

library = env.SharedLibrary(
    "project/addons/bocchitracker-gd/bin/"
    + libexportfolder
    + "bocchitracker_binding"
    + debugsuffix,
    source=sources,
)

Default(library)